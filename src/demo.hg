root -> method+
_ -> `\s*`

type -> ('void' / 'int' / 'double' / 'bool') '[]'*
name -> !type `[_a-zA-Z]\w*`

parameters -> (type name (',' type name)*)?
method:'method' -> type name '(' parameters ')' (block_statement / lambda_body)

lambda_body:'lambda' -> '=>' expr ';'

statement ->
    declaration_statement /
    embedded_statement

declaration:'declaration' -> type name ('=' expr)?
declaration_statement:'declaration_statement' -> declaration ';'

block_statement:'block_statement' -> '{' statement* '}'
return_statement:'return_statement' -> 'return' expr ';'

for_statement:'for_statement' -> 'for' '(' (declaration / expr:'expr')? ';' expr? ';' expr? ')' embedded_statement
while_statement:'while_statement' -> 'while' '(' expr ')' embedded_statement
do_while_statement:'do_while_statement' -> 'do' embedded_statement 'while' '(' expr ')' ';'
if_else_statement:'if_else_statement' -> 'if' '(' expr ')' embedded_statement ('else' embedded_statement)?
break_statement:'break_statement' -> 'break' ';'
continue_statement:'continue_statement' -> 'continue' ';'

expr_statement:'expr_statement' -> expr:'expr' ';'

embedded_statement ->
    block_statement /
    return_statement /
    for_statement /
    while_statement /
    do_while_statement /
    if_else_statement /
    break_statement /
    continue_statement /
    expr_statement

expr ->
    [
        #WantOperand (Nullary, Prefix)
        '()'         none none '(' expr ')'
        'new[]'      none none 'new' type '[' expr ']'
        'sizeof'     none none 'sizeof' '(' expr ')'
        'u+'         none 3    '+'
        'u-'         none 3    '-'
        '~'          none 3    '~'
        'real'       none none `\d+\.\d+`
        'integral'   none none `\d+`
        'boolean'    none none 'true' / 'false'
        'identifier' none none `[_a-zA-Z]\w*`

        #HaveOperand (Postfix, Infix)
        '.'          0    0    '.'
        '$'          1    none '(' (expr (',' expr)*)? ')'
        '[]'         1    none '[' expr ']'
        'post++'     1    none '++'
        'post--'     1    none '--'
        '*'          4    4    '*'
        '/'          4    4    '/'
        '+'          5    5    '+'
        '-'          5    5    '-'
        '<='         6    6    '<='
        '>='         6    6    '>='
        '<'          6    6    '<'
        '>'          6    6    '>'
        '=='         7    7    '=='
        '!='         7    7    '!='
        '&'          8    8    '&'
        '^'          9    9    '^'
        '|'          10   10   '|'
        '='          2    99   '='
        '?:'         11   99   '?' expr ':'
    ]
