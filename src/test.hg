root -> method:'method'
_ -> `\s*`

type -> 'int' / 'double' / 'bool'
name -> !type `[_a-zA-Z]\w*`

parameters -> (type name (',' type name)*)?
method -> type name '(' parameters ')' (block_statement / lambda_body)

lambda_body:'lambda' -> '=>' expr ';'

statement ->
    declaration_statement /
    embedded_statement

declaration:'declaration' -> type name ('=' expr)?
declaration_statement:'declaration_statement' -> declaration ';'

block_statement:'block_statement' -> '{' statement* '}'
return_statement:'return_statement' -> 'return' expr ';'
expr_statement:'expr_statement' -> expr ';'

embedded_statement ->
    block_statement /
    return_statement /
    expr_statement

expr ->
    [
        #WantOperand (Nullary, Prefix)
        '()'         none none '(' expr ')'
        'u+'         none 1    '+'
        'u-'         none 1    '-'
        '~'          none 1    '~'
        'real'       none none `\d+\.\d+`
        'integral'   none none `\d+`
        'boolean'    none none 'true' / 'false'
        'identifier' none none `[_a-zA-Z]\w*`

        #HaveOperand (Postfix, Infix)
        '$'          0    none '(' (expr (',' expr)*)? ')'
        '*'          3    3    '*'
        '/'          3    3    '/'
        '+'          4    4    '+'
        '-'          4    4    '-'
        '<='         5    5    '<='
        '>='         5    5    '>='
        '<'          5    5    '<'
        '>'          5    5    '>'
        '=='         6    6    '=='
        '!='         6    6    '!='
        '&'          7    7    '&'
        '^'          8    8    '^'
        '|'          9    9    '|'
        '='          0    99   '='
        '?:'         10   99   '?' expr ':'
        '!'          2    none '!'
    ]
